import? 'scripts/languages.just'
mod? hosts 'scripts/hosts.just'

default:
    just --list

update: update-config sync-projects

update-config:
    -chezmoi update
    # nix flake update
    cd ~/nixos-config && nix run .#build-switch

update-pkgs:
    -cargo install garden-tools
    -curl -L --proto '=https' --tlsv1.2 -sSf https://raw.githubusercontent.com/cargo-bins/cargo-binstall/main/install-from-binstall-release.sh | bash
    -cargo binstall --strategies crate-meta-data jj-cli

sync: sync-projects sync-overleaf

sync-projects:
    garden grow "*"
    -garden cmd "*" pull -j4
    garden cmd "*" status

[private]
sync-overleaf:
    just -f projects/share/justfile sync-overleaf

backup-config:
    #!/usr/bin/env sh
    mackup backup --force && mackup uninstall --force

backup-pictures:
    rclone sync ~/Pictures BoxUCLA:Pictures --progress

# restoring the configuration using mackup
restore-config:
    #!/usr/bin/env sh
    echo "Syncing applications with 'mackup'..."
    mackup restore --force && mackup uninstall --force

[private]
migrate:
    rclone sync GoogleDriveUCLA:library BoxUCLA:library --progress
    ln -s "/Volumes/My Passport for Mac/data" ~/data

clean:
    -nix-collect-garbage -d
    -brew cleanup
    -pixi clean cache --yes
    -micromamba clean --all

[private]
disk-usage:
    gdu --ignore-dirs-pattern '.*CloudStorage'
    gdu / -I '.*CloudStorage'

check-version:
    -nix --version
    -brew --version
    -micromamba --version
