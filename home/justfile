import? 'scripts/languages.just'
mod? hosts 'scripts/hosts.just'

default:
    just --list

update: update-config sync-projects

update-config:
    -chezmoi update
    cd ~/nixos-config && nix run .#build-switch

update-pkgs:
    -cargo install garden-tools

sync-projects:
    garden grow "*"
    -garden cmd "*" pull -j
    garden cmd "*" status

backup-config:
    #!/usr/bin/env sh
    mackup backup --force && mackup uninstall --force

backup-pictures:
    rclone sync ~/Pictures BoxUCLA:Pictures --progress

# restoring the configuration using mackup
restore-config:
    #!/usr/bin/env sh
    echo "Syncing applications with 'mackup'..."
    mackup restore --force && mackup uninstall --force

[private]
migrate:
    rclone sync GoogleDriveUCLA:library BoxUCLA:library --progress
    ln -s "/Volumes/My Passport for Mac/data" ~/data

clean:
    -nix-collect-garbage -d
    -brew cleanup

check-version:
    -nix --version
    -brew --version
    -micromamba --version
